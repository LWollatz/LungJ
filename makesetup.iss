; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=LungJ
AppVersion=0.3
VersionInfoVersion=0.3.0.0
OutputBaseFilename=LungJwinsetup-0.3

AppCopyright=Copyright (C) 2015 Lasse Wollatz
AppComments=Fiji Plugin
AppContact=Lasse Wollatz, University of Southampton
AppPublisher=University of Southampton
AppPublisherURL=https://bitbucket.org/lwollatz/lungj/
AppSupportURL=https://bitbucket.org/lwollatz/lungj/wiki/Home

WizardImageFile=logoleft.bmp
WizardSmallImageFile=logosmall.bmp

; CreateUninstallRegKey=no
DirExistsWarning=no
OutputDir=.
DefaultDirName={src}
Uninstallable=no

[Messages]
WelcomeLabel2=This will install [name/ver] on your computer.%n%nYou need to have Fiji with ImageJ version 1.49n or later and Java 8 (jre1.8) installed.

[Components]
Name: PlugIn; Description: "LungJ PlugIn for Fiji"; Types: full compact custom; Flags: fixed
Name: Classifiers; Description: "Basic set of WEKA classifier models"; Types: full custom

[Files]
Source: "LungJ_.jar"; DestDir: "{app}\plugins\LungJ"; Components:PlugIn; Flags: promptifolder
Source: "classifiers\vessels.model"; DestDir: "{app}\plugins\LungJ"; Components:Classifiers
Source: "classifiers\background.model"; DestDir: "{app}\plugins\LungJ"; Components:Classifiers
Source: "classifiers\tissue.model"; DestDir: "{app}\plugins\LungJ"; Components:Classifiers

[Run]
Filename: "{app}\{code:getImageJ}"; Description: "Run ImageJ now"; Flags: postinstall
;Filename: "{app}\ImageJ-win64.exe"; Description: "Start ImageJ win64"; Flags: postinstall


[Code]
var 
  hasIJ : Boolean;
  outfilename: String;

function FileExistsWildcard(const FileName: string): String;
var
  FindRec    : TFindRec;
  exists     : Boolean;
begin
  exists := False;
  Result := '';
  if FindFirst(FileName, FindRec) then
  try
    exists := FindRec.Attributes and FILE_ATTRIBUTE_DIRECTORY = 0;
    Result := FindRec.Name;
  finally
    FindClose(FindRec);
  end;
end;

function getImageJ(): String;
begin
  Result := FileExistsWildcard(ExpandConstant('{app}') + '\ImageJ*.exe');
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  hasIJ := True;
  if (CurPageID = 6) then begin
    outfilename := FileExistsWildcard(ExpandConstant('{app}') + '\ImageJ*.exe');
    if outfilename = '' then begin
      hasIJ := False;
    end;
    if hasIJ then begin
      MsgBox('Found ' + outfilename, mbInformation, mb_Ok);
    end;
    //hasIJ := FileExists(ExpandConstant('{app}') + '\ImageJ-win64.exe');
    //if not hasIJ then begin
    //  hasIJ := FileExists(ExpandConstant('{app}') + '\ImageJ-win32.exe');
    //end
  end;
  if not hasIJ then begin
     MsgBox('Could not find ImageJ in specified Folder', mbInformation, mb_Ok); 
  end;
  Result := hasIJ;
end;